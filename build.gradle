buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.1.0'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven'

apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/markdown-git-changelog.gradle'

def ENV = System.getenv()
version = ENV.BUILD_NUMBER ? "${mod_version}.${ENV.BUILD_NUMBER}" : "${mod_version}.custom"
group = package_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
	version = "${mc_version}-${forge_version}"
	runDir = "run"
	mappings = "${mappings_version}"

	replace "0.0.0.ftbquests", project.version
	replaceIn "${mod_reference}"
}

configurations.all {
	transitive = false
}

repositories {
	mavenLocal()
	maven { url "https://maven.latmod.com/" } //FTBLib
	maven { url "https://minecraft.curseforge.com/api/maven/" } //ProjectE
	maven { url "http://maven.ic2.player.to/" } //IC2
	maven { url "https://maven.blamejared.com/" } //PackMode & Botania
	maven { url "http://maven.thiakil.com/" } //Baubles
	maven { url "https://mod-buildcraft.com/maven/" } //Buildcraft
	maven { url "http://dvs1.progwml6.com/files/maven" } //JEI
}

dependencies {
	deobfCompile "com.feed_the_beast.mods:FTBLib:${ftblib_version}"
	deobfCompile "com.feed_the_beast.mods:FTBUtilities:${ftbutlities_version}"
	deobfCompile "com.latmod.mods:ItemFilters:${itemfilters_version}"
	deobfCompile "projecte:ProjectE:1.12:${projecte_version}"
	provided "net.industrial-craft:industrialcraft-2:${ic2_version}:api"
	deobfCompile "io.sommers:packmode:${packmode_version}"
	deobfProvided "vazkii.botania:Botania:${botania_version}"
	deobfProvided "com.azanor.baubles:Baubles:${baubles_version}"
	provided "com.mod-buildcraft:buildcraft-api:${buildcraft_version}"
	compile "mezz.jei:jei_${mc_version}:${jei_version}:api"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

artifacts {
	archives sourceJar
}

if (ENV.LOCAL_MAVEN) {
	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: "file://${ENV.LOCAL_MAVEN}")
			}
		}
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = "${curseforge_id}"
			releaseType = "release"
			addGameVersion "${mc_version}"
			relations {
				requiredDependency 'ftblib'
				requiredDependency 'item-filters'
				optionalDependency 'packmode'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
			addArtifact(sourceJar)
		}
	}
}