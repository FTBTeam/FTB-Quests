buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.1.0'
		classpath 'com.wynprice.cursemaven:CurseMaven:1.2.2'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven'
apply plugin: 'com.wynprice.cursemaven'

apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/markdown-git-changelog.gradle'

def ENV = System.getenv()
version = ENV.BUILD_NUMBER ? "${mod_version}.${ENV.BUILD_NUMBER}" : "${mod_version}.custom"
group = package_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
	version = "${mc_version}-${forge_version}"
	runDir = "run"
	mappings = project.mappings_version

	replace "0.0.0.ftbquests", project.version
	replaceIn project.mod_reference
}

configurations.all {
	transitive = false
}

repositories {
	mavenLocal()
	maven { url "https://maven.latmod.com/" } //FTB Library, KubeJS, Item Filters, FTB Utilities
}

dependencies {
	deobfCompile "com.feed_the_beast.mods:FTBLib:${ftblib_version}"
	deobfCompile "com.latmod.mods:ItemFilters:${itemfilters_version}"
	deobfProvided "dev.latvian.kubejs:KubeJS:forge-1.12.2-${kubejs_version}"
	deobfProvided "com.feed_the_beast.mods:FTBUtilities:${ftbutilities_version}"
	deobfProvided curse.resolve('jei', project.jei_file)
	deobfProvided curse.resolve('industrial-craft', project.industrialcraft_file)
	deobfProvided curse.resolve('packmode', project.packmode_file)
	deobfProvided curse.resolve('botania', project.botania_file)
	deobfProvided curse.resolve('baubles', project.baubles_file)
	deobfProvided curse.resolve('buildcraft-core', project.buildcraftcore_file)
	deobfProvided curse.resolve('projecte', project.projecte_file)
	deobfProvided curse.resolve('game-stages', project.gamestages_file)
	deobfProvided curse.resolve('custom-npcs', project.customnpcs_file)
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

artifacts {
	archives sourceJar
}

if (ENV.LOCAL_MAVEN) {
	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: "file://${ENV.LOCAL_MAVEN}")
			}
		}
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id
			releaseType = "release"
			addGameVersion project.mc_version
			relations {
				requiredDependency 'ftblib'
				requiredDependency 'item-filters'
				optionalDependency 'kubejs'
				optionalDependency 'ftb-utilities'
				optionalDependency 'jei'
				optionalDependency 'industrial-craft'
				optionalDependency 'packmode'
				optionalDependency 'botania'
				optionalDependency 'baubles'
				optionalDependency 'buildcraft-core'
				optionalDependency 'projecte'
				optionalDependency 'game-stages'
				optionalDependency 'custom-npcs'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
			addArtifact(sourceJar)
		}
	}
}