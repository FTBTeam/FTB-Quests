plugins {
	id "com.github.johnrengelman.shadow" version "5.0.0"
	id "com.matthewprenger.cursegradle"
}

def ENV = System.getenv()

configurations {
	shadowCommon
}

architectury {
	platformSetupLoomIde()
	forge()
}

loom {
	mixinConfigs = [/*"ftbquests.mixins.json", */ "ftbquests-common.mixins.json"]
	useFabricMixin = true
}

dependencies {
	minecraft "com.mojang:minecraft:${rootProject.architectury.minecraft}"
	mappings minecraft.officialMojangMappings()
	forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
	modApi "me.shedaniel:architectury-forge:${rootProject.architectury_version}"

	modImplementation "curse.maven:jei-238222:${jei_file}"
	modImplementation "dev.ftb.mods:ftb-library-forge:${rootProject.ftb_library_version}"
	modImplementation "dev.ftb.mods:ftb-teams-forge:${rootProject.ftb_teams_version}"
	modImplementation "curse.maven:item-filter-309674:${rootProject.itemfilters_version_forge}"
	modImplementation "dev.latvian.mods:kubejs-forge:${rootProject.kubejs_version}"
	modImplementation "dev.latvian.mods:rhino:${rootProject.rhino_version}"
	modImplementation "curse.maven:bookshelf-228525:${bookshelf_file}"
	modImplementation "curse.maven:game-stages-268655:${gamestages_file}"
	modImplementation "curse.maven:ctm-267602:3137659"

	implementation(project(path: ":common")) {
		transitive = false
	}
	developmentForge(project(path: ":common")) {
		transitive = false
	}
	shadowCommon(project(path: ":common", configuration: "transformProductionForge")) {
		transitive = false
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}

shadowJar {
	exclude "fabric.mod.json"

	configurations = [project.configurations.shadowCommon]
	classifier "dev-shadow"
}

remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	archiveBaseName.set "${rootProject.mod_name}-${project.name}"
	archiveClassifier.set null
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}

	classifier "dev"
}

java {
	withSourcesJar()
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id_forge
			releaseType = project.curseforge_type
			addGameVersion "Forge"
			addGameVersion "1.16.4"
			addGameVersion "1.16.5"
			mainArtifact(remapJar.archivePath)
			relations {
				requiredDependency 'architectury-forge'
				requiredDependency 'ftb-teams-forge'
				requiredDependency 'ftb-library-forge'
				requiredDependency 'item-filters-forge'
				optionalDependency 'kubejs-forge'
				optionalDependency 'jei'
				optionalDependency 'game-stages'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}

publishing {
	publications {
		mavenFabric(MavenPublication) {
			artifactId = rootProject.archivesBaseName + "-" + project.name
			artifact(remapJar)
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		if (ENV.LOCAL_MAVEN) {
			maven {
				url "file://${ENV.LOCAL_MAVEN}"
			}
		}

		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/minecraft"
				credentials {
					username = "ftb"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}

rootProject.tasks.getByName("curseforgePublish").dependsOn tasks.getByName("curseforge")
